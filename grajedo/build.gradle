description = "GraJeDo"
apply plugin: "java"
apply plugin: 'application'

buildDir = "gradle-build"
archivesBaseName = "grajedo"
applicationName = "grajedo"
mainClassName = "main.GraJeDo"
applicationDefaultJvmArgs = ["-d64 -Xms256m -Xmx4000m"]

sourceSets {
   main {
      java {
         srcDir "src"
      }
   }
	
   test {
      java {
         srcDir "test"
      }
   }
}

dependencies {
    compile fileTree(dir: "lib", include: "*.jar")
}

test {
    exclude 'production/**'
}

task productionTest( type: Test ) {
    include 'production/**'
}

tasks.withType(Test) {
    testLogging {
        events "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
